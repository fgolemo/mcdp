template [Battery: `BatteryInterface, Actuation: `ActuationInterface] 
mcdp {  
  provides travel_distance [km]
  provides num_missions [R]
  provides carry_payload [g] 

  requires total_cost_ownership [$]

  strategy = new strategy
 
  actuation_energetics = 
     specialize[Battery:Battery, Actuation:Actuation] 
     `ActuationEnergeticsTemplate

  actuation_energetics.endurance >= strategy.endurance
  actuation_energetics.velocity >= strategy.velocity
  actuation_energetics.num_missions >= num_missions
  actuation_energetics.extra_payload >= carry_payload
  strategy.distance >= travel_distance

  computer = new Computer 
  perception = new Perception 
  computer.computation >= perception.computation

  sensor =  new Sensor 
  sensor.resolution >= perception.camera_resolution
  sensor.framerate >= perception.camera_framerate
  sensor.fov >= perception.camera_fov
  
  perception.velocity >= strategy.velocity
  actuation_energetics.extra_power >= computer.power + sensor.power

  # We can take into account the shipping cost
  shipping = new Shipping
  
  total_mass = actuation_energetics.total_mass 

  shipping.ships >= total_mass

  total_cost_ownership >= shipping.postage + actuation_energetics.total_cost
}