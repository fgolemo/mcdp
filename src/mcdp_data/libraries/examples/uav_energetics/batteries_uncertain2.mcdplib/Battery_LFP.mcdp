mcdp {
    provides capacity [J]
    provides missions [R]

    requires mass     [g]
    requires cost     [USD]

    # Number of replacements
    requires maintenance [R]

    # Battery properties
    specific_energy_inv = Uncertain(1.0 []/ 99. Wh/kg, 1.0 [] /  81 Wh/kg)
    specific_cost_inv = Uncertain(1.0 [] / 1.65 Wh/USD, 1.0 [] / 1.35 Wh/USD)
    cycles_inv = Uncertain(1.0 []/1650.0 [], 1.0[]/ 1350.0 [])

    # Constraint between mass and capacity
    massc = provided capacity * specific_energy_inv

    # How many times should it be replaced?
    num_replacements = ceil(provided missions * cycles_inv)
    required maintenance >= num_replacements

    # Cost is proportional to number of replacements
    costc = (provided capacity * specific_cost_inv) * num_replacements

    required cost >= costc
    required mass >= massc
}

