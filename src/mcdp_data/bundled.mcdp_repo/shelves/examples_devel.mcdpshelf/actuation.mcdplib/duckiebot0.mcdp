 # mcdp-plot --cache --plots ndp_graph_enclosed -D ../.. -d . duckiebot0

 mcdp { 
  dagu_chassis = instance `DaguChassis
  battery_ravpower = instance `BatteryRavpower

  rpi = instance `RaspberryPI2
  dc_hat = instance `AdafruitDCHat

  pi_camera = instance `PICamera

  usb_to_wires = instance `USB_to_wires

  gpiocom required by rpi ≼ com provided by pi_camera

  cable = instance `USB_A_to_micro_B_cable

  USBMicroCharger = instance `USBMicroCharging

  requires ac = in required by USBMicroCharger

  in required by battery_ravpower ≼ out provided by USBMicroCharger

  in required by rpi ≼ out provided by cable
  in required by cable ≼ out1 provided by battery_ravpower

  # battery below
  payload required by battery_ravpower ≼ payload_bottom provided by dagu_chassis

  in required by usb_to_wires ≼ out2 provided by battery_ravpower
  in required by dc_hat ≼ out provided by usb_to_wires

  fit = instance template mcdp {
      provides p1 [`Payload]
      provides p2 [`Payload]
      provides p3 [`Payload]
      #provides p4 [`Payload]
      #provides p5 [`Payload]
      #provides p6 [`Payload]
      #provides p7 [`Payload]
      requires payload [`Payload]
  }


  p1 provided by fit ≽ payload required by rpi
  p2 provided by fit ≽ payload required by dc_hat
  p3 provided by fit ≽ payload required by pi_camera

  payload required by fit ≼ payload_top provided by dagu_chassis

  provides motion using dagu_chassis

  pisocketcom1 required by dc_hat ≼ pisocketcom provided by rpi
  ignore pisocketcom1 provided by dc_hat
  ignore ethcom provided by rpi
  ignore usbcom1 required by rpi
  ignore ethcom required by rpi

  out1 provided by dc_hat ≽ pwm1 required by dagu_chassis
  out2 provided by dc_hat ≽ pwm2 required by dagu_chassis
  ignore out3 provided by dc_hat
  ignore out4 provided by dc_hat

  requires budget [USD]

  required budget ≽ (
    budget required by dagu_chassis +
     budget required by battery_ravpower +
     # budget required by buffalo +
     budget required by rpi +
     budget required by dc_hat +
     budget required by pi_camera +
     budget required by usb_to_wires +
     budget required by USBMicroCharger
  )
}
